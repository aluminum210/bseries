plugins {
  /* https://github.com/b3er/gradle-local-properties-plugin */
  id "com.github.b3er.local.properties" version "1.1"
}

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

subprojects {

apply plugin: 'base'

task('copyResources', type: Copy, group: 'Custom') {
  from 'src/main/resources'
  exclude "**/*~"
  into "${buildDir}/tmp/${project.name}"

  /* Work-around for multi-project builds. */
  gradle.projectsEvaluated {
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: [version: project.version])
    filteringCharset = 'UTF-8'
  }
}

task('copySnippets', type: Copy, group: 'Custom') {
  println('Copying...')
  into "${buildDir}/tmp/${project.name}"
  from 'src/main/lua'
  exclude "**/*~"
  include "**/*.lua"
}

task('distZip', type: Zip, group: 'Custom') {
  from fileTree("${buildDir}/tmp/")
  destinationDir file("${distsDir}")
}


task('deployLocal', type: Copy, group: 'Custom') {
  /* Work-around for multi-project builds. */
  gradle.projectsEvaluated {
    from zipTree("${distsDir}/${distZip.archiveName}")
    /* 
     * `addonsDir` is defined in `local.properties` file. 
     * Example:
     * local.properties:
     *   addonsDir=D:/games/wow/Interface/AddOns/
     * Remember to use Java compatible URIs for file paths!
     */   
    into addonsDir
  }
}

task('ldoc', type: Exec, group: 'Custom') {
    /* 
     * `ldocExecutable` is defined in `local.properties` file. 
     * Example:
     * local.properties:
     *   ldocExecutable=D:/opt/chocolatey/lib/luarocks/luarocks-2.4.4-win32/systree/bin/ldoc.bat
     * Remember to use Java compatible URIs for file paths!
     */   
	executable ldocExecutable
	args "${buildDir}/tmp/${project.name}/", '--all', '--dir', "${buildDir}/tmp/${project.name}/doc/"
}

task('luacheck', type: Copy, group: 'Custom') {
	from "${buildDir}/tmp/${project.name}/"
	into "${buildDir}/tmp/${project.name}/"
	include '**/*.lua'
    /* 
     * `luacheckExecutable` is defined in `local.properties` file. 
     * Example:
     * local.properties:
     *   luacheckExecutable=luacheck
     * Remember to use Java compatible URIs for file paths!
     */   
	eachFile{fileDetails -> 
		exec{
			executable luacheckExecutable
			args fileDetails.file
		}
		fileDetails.exclude()
	}
}

copySnippets.shouldRunAfter(clean)
copyResources.shouldRunAfter(clean)
ldoc.shouldRunAfter(clean)
distZip.shouldRunAfter(clean)
assemble.shouldRunAfter(clean)
luacheck.dependsOn(copySnippets)
luacheck.mustRunAfter(copySnippets)
ldoc.dependsOn(copySnippets)
ldoc.mustRunAfter(copySnippets)
ldoc.shouldRunAfter(luacheck)
distZip.mustRunAfter(copySnippets)
distZip.mustRunAfter(copyResources)
distZip.mustRunAfter(ldoc)
deployLocal.mustRunAfter(assemble)
assemble.dependsOn(copySnippets)
assemble.dependsOn(copyResources)
assemble.dependsOn(ldoc)
assemble.dependsOn(luacheck)
assemble.dependsOn(distZip)

}